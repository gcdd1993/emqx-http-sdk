plugins {
    id 'emqx-http-sdk.java-common-conventions'
    id 'java-library'
    id "maven-publish"
}

dependencies {
    constraints {
        api("com.squareup.retrofit2:retrofit:$retrofit_version")
        api("com.squareup.retrofit2:converter-jackson:$retrofit_version")
    }
}

def snapshotArtifactory = 'https://s01.oss.sonatype.org/content/repositories/snapshots'
def releaseArtifactory = 'https://s01.oss.sonatype.org/content/repositories/releases'
def mavenName = 'gcdd1993'
afterEvaluate { project ->
    if (project.plugins.hasPlugin("maven-publish")) {
        publishing {
            repositories {
                maven {
                    name "${mavenName}"
                    url = version.endsWith('SNAPSHOT') ? snapshotArtifactory : releaseArtifactory
                    credentials {
                        username "${ossrh_username}"
                        password "${ossrh_password}"
                    }
                    authentication {
                        basic(BasicAuthentication)
                    }
                }
            }
            publications {
                emqxJavaSdk(MavenPublication) {
                    from components.java
                    versionMapping {
                        usage('java-api') {
                            fromResolutionOf('runtimeClasspath')
                        }
                        usage('java-runtime') {
                            fromResolutionResult()
                        }
                    }
                }
            }
        }
    }
}
